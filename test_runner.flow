import sys/process;
import fs/filesystem;
import net/url_parameter;
import text/blueprint;

runTest(file: string, err: bool) -> bool {
	// Compile to bytecode
	out_c = execSystemProcessOutput("java", ["-jar", "simc.jar", "file=" + file, "show-all=1"], ".");
	// Execute the bytecode
	out_e = execSystemProcessOutput("java", [
		"-jar", "sime.jar", "file=" + changeFileExt(file, ".binsl"),
		"in=5,5,5,5,5" // To keep it simple - pass as input always 5
	], ".");
	if (isUrlParameterTrue("show-out")) {
		println("Test: " + file + "\n---------------------------\n" + out_c.stdall + "\n" + out_e.stdall);
	}
	// If an error is expected - it must happen:
	success = strContains(out_c.stdall + out_e.stdall, "error:") == err;
	if (success) {
		println("Test: " + file + " PASSED");
	} else {
		println("Test: " + file + " FAILED:\n" + out_c.stdall + "\n" + out_e.stdall);
	}
	success;
}

main() {
	tests_dir = getUrlParameterDef("tests", "tests");
	test_files = filter(readDirectory(tests_dir), \f -> endsWith(f, ".sl"));
	println("going to run tests:\n" + superglue(test_files, \f -> "\t" + f, "\n"));
	failed = fold(test_files, [], \acc, file -> {
		name = changeFileExt(fileNameOnly(file), "");
		err = endsWith(name, "_err");
		if (runTest(pathCombine(tests_dir, file), err)) acc else arrayPush(acc, file);
	});
	if (failed == []) {
		println("All tests pass successfully :)");
	} else {
		println("Failed tests:\n" + superglue(failed, \f -> "\t" + f, "\n"));
	}
	quit(0);
}
