import ds/array;
import runtime;
import text/blueprint;
import string_utils;
import sys/system;
import err;
import bc;
import io;
import util;

export {

// Run a program (i.e. execute the bytecode).
execBcCode(prog: BcCode, trace: bool, err: Err, io: Io) -> void;

}

// Runtime values, which occur during execution
Val ::= ValBool, ValInt, ValString, ValErr;
	ValBool(b: bool);
	ValInt(i: int);
	ValString(s: string);
	ValErr(); // Runtime error value

// The global state og the program.
State(
	vars: [Val],      // state of local variables
	pc: int,          // program counter - the index of a current command
	stack: List<Val>  // stack, which is used for evaluation of expressions
);

// Check that all locals indexes, used in bytecode are within boundaries.
verifyBcCode(prog: BcCode, err: Err) -> bool {
	check_i = \i, c -> if (i < 0 || i >= prog.locals) {
		err.report("illegal var index in: " + comm2s(c));
	}
	iter(prog.code, \c -> switch (c) {
		BcPush(i):       check_i(i, c);
		BcPop(i):        check_i(i, c);
		BcReadBool(i):   check_i(i, c);
		BcReadInt(i):    check_i(i, c);
		BcReadString(i): check_i(i, c);
		default: {}
	});
	isOk(err);
}

execBcCode(prog: BcCode, trace: bool, err: Err, io: Io) -> void {
	if (verifyBcCode(prog, err)) {
		initial = State(
			// All variables initially are set to some default values - no matter which particularily.
			generate(0, prog.locals, \__ -> ValInt(0)), 
			0, makeList()
		);
		if (trace) {
			println("initial state: " + state2s(initial));
		}
		// Iterate the change of state until program counter goes out of program or error occurs.
		ignore(for(initial,
			\s -> isOk(err) && s.pc < length(prog.code),
			\prev -> {
				// A single step of execution: action of a command. 
				next = execComm(prog.code[prev.pc], prev, err, io);
				if (trace) {
					println(comm2s(prog.code[prev.pc]) + ":\n\t" + state2s(next));
				}
				next;
			}
		));
	}
}

execComm(comm:  BcComm, s: State, err: Err, io: Io) -> State {
	// Increment the program counter.
	incPc = \x -> State(x with pc = x.pc + 1);
	// Push some value on a stack.
	push = \x, v -> State(x with stack = Cons(v, x.stack));
	// Pop value from the stack. Note: a Pair is returned, not a single value!
	pop = \x -> switch (x.stack) {
		Cons(h, t): Pair(h, State(x with stack = t));
		EmptyList(): { err.report("popping empty list"); Pair(ValErr(), x); }
	}
	switch (comm) {
		BcPush(i): {
			// Push the value of an 'i'-th local variable on stack.
			incPc(push(s, s.vars[i]));
		}
		BcPop(i): {
			// Pop the value from stack an set it to the 'i'-th local variable.
			p = pop(s);
			incPc(State(p.second with vars = replace(s.vars, i, p.first)));
		}
		BcPrint(): {
			// Pop the value from stack and send it to the output sink.
			p = pop(s);
			io.out(val2s(p.first));
			incPc(p.second);
		}
		BcReadBool(i): {
			// Read bool value from io to the to the 'i'-th local variable.
			incPc(State(s with vars = replace(s.vars, i, ValBool(s2b(io.in())))));
		}
		BcReadInt(i): {
			// Read int value from io to the to the 'i'-th local variable.
			incPc(State(s with vars = replace(s.vars, i, ValInt(s2i(io.in())))));
		}
		BcReadString(i): {
			// Read string value from io to the to the 'i'-th local variable.
			incPc(State(s with vars = replace(s.vars, i, ValString(io.in()))));
		}
		BcIfNot(sh): {
			p = pop(s);
			switch (p.first) {
				ValBool(c): {
					// Pop the boolean value from stack, and if not true, 
					// shift the program counter by 'sh'. Otherwise shift by 1 (increment).s
					State(p.second with pc = s.pc + if (c) 1 else sh);
				}
				default: {
					err.report("non-boolean condition: " + val2s(p.first));
					s;
				}
			}
		}
		BcGoto(sh): {
			// Shift a program counter by 'sh'.
			State(s with pc = s.pc + sh);
		}
		BcConstBool(b): {
			// Push a constant bool value onto the stack.
			incPc(push(s, ValBool(b)));
		}
		BcConstInt(i): {
			// Push a constant int value onto the stack.
			incPc(push(s, ValInt(i)));
		}
		BcConstString(s1): {
			// Push a constant string value onto the stack.
			incPc(push(s, ValString(s1)));
		}
		BcOp(op, arity): {
			// Pop arguments from stack 'arity' times and collect them on the second component of triple.
			// The first component of a triple is state after popping args.
			t = for(Triple(s, [], 0),
				\x -> x.third < arity,
				\x -> {
					p = pop(x.first);
					Triple(p.second, arrayPush(x.second, p.first), x.third + 1);
				}
			);
			// Execute an operation and push the result back onto the stack.
			incPc(push(t.first, execOp(op, t.second, err)));
		}
	}
}

val2int(v: Val, err: Err) -> int {
	switch (v) {
		ValInt(i): i;
		default: {
			err.report("integer expected, while: " + val2s(v) + " is met"); 0;
		}
	}
}

val2bool(v: Val, err: Err) -> bool {
	switch (v) {
		ValBool(b): b;
		default: {
			err.report("boolean expected, while: " + val2s(v) + " is met"); false;
		}
	}
}

val2string(v: Val, err: Err) -> string {
	switch (v) {
		ValString(s): s;
		default: {
			err.report("string expected, while: " + val2s(v) + " is met"); "";
		}
	}
}

// Performs a single operation on 'args'.
execOp(op: string, args: [Val], err: Err) -> Val {
	check_arity = \arity -> if (length(args) != arity) {
		err.report("wrong arity of " + op + ", expected: " + i2s(arity) + " wile got: " + i2s(length(args)));
	}
	int_bin_op = \int_op -> {
		check_arity(2);
		ValInt(int_op(val2int(args[0], err), val2int(args[1], err)));
	}
	cmp_bin_op = \bool_op, int_op, str_op -> {
		check_arity(2);
		switch (valType(args[0])) {
			TypeBool():   ValBool(bool_op(val2bool(args[0], err), val2bool(args[1], err)));
			TypeInt():    ValBool(int_op(val2int(args[0], err), val2int(args[1], err)));
			TypeString(): ValBool(str_op(val2string(args[0], err), val2string(args[1], err)));
			default:      ValErr();
		}
	}
	// Set of conversions from one base type to another.
	if (op == "i2s") {
		check_arity(1);
		ValString(i2s(val2int(args[0], err)));
	} else if (op == "s2i") {
		check_arity(1);
		ValInt(s2i(val2string(args[0], err)));
	} else if (op == "b2s") {
		check_arity(1);
		ValString(b2s(val2bool(args[0], err)));
	} else if (op == "s2b") {
		check_arity(1);
		ValBool(s2b(val2string(args[0], err)));
	} else if (op == "i2b") {
		check_arity(1);
		ValBool(val2int(args[0], err) != 0);
	} else if (op == "b2i") {
		check_arity(1);
		ValInt(if (val2bool(args[0], err)) 1 else 0);
	} else
	// Unary boolean operations.
	if (op == "neg") {
		check_arity(1);
		ValInt(-val2int(args[0], err));
	} else if (op == "not") {
		check_arity(1);
		ValBool(!val2bool(args[0], err));
	} else
	// Special overloaded operation '+': act on integers AND strings.
	if (op == "+") {
		switch (valType(args[0])) {
			TypeInt():    ValInt(fold(args, 0, \acc, v -> acc + val2int(v, err)));
			TypeString(): ValString(fold(args, "", \acc, v -> acc + val2string(v, err)));
			default: {
				err.report("+ operands must be of integer or string type, got: " + val2s(args[0])); ValInt(0);
			}
		}
	} else
	// Binary arhithmetics.
	if (op == "-")  int_bin_op(\x, y -> x - y) else
	if (op == "*")  int_bin_op(\x, y -> x * y) else
	if (op == "/")  int_bin_op(\x, y -> x / y) else
	if (op == "%")  int_bin_op(\x, y -> x % y) else
	// Comparison.
	if (op == "==") cmp_bin_op(\x, y -> x == y, \x, y -> x == y, \x, y -> x == y) else
	if (op == "!=") cmp_bin_op(\x, y -> x != y, \x, y -> x != y, \x, y -> x != y) else
	if (op == "<")  cmp_bin_op(\x, y -> x <  y, \x, y -> x <  y, \x, y -> x <  y) else
	if (op == "<=") cmp_bin_op(\x, y -> x <= y, \x, y -> x <= y, \x, y -> x <= y) else
	if (op == ">")  cmp_bin_op(\x, y -> x >  y, \x, y -> x >  y, \x, y -> x >  y) else
	if (op == ">=") cmp_bin_op(\x, y -> x >= y, \x, y -> x >= y, \x, y -> x >= y) else
	// Boolean operations.
	if (op == "&&") ValBool(fold(args, true, \acc, v -> acc && val2bool(v, err))) else 
	if (op == "||") ValBool(fold(args, false, \acc, v -> acc || val2bool(v, err))) else {
		err.report("unsupported operation: " + op); ValInt(0);
	}
}

state2s(s: State) -> string {
	"pc = " + i2s(s.pc) + " " + 
	"vars = [" + superglue(s.vars, val2s, ", ") + "] "
	"stack = {" + superglue(list2array(s.stack), val2s, ", ") + "}";
}

val2s(v: Val) -> string {
	switch (v) {
		ValBool(b):   b2s(b);
		ValInt(i):    i2s(i);
		ValString(s): s; 
		ValErr():     "BROKEN VALUE";
	}
}

valType(v: Val) -> Type {
	switch (v) {
		ValBool(__):   TypeBool();
		ValInt(__):    TypeInt();
		ValString(__): TypeString();
		ValErr():      TypeInt();
	}
}
