import ds/tree;
import string;
import math/bits;
import err;
import bytecode;

export {

/* Bytecode binary format:

1) integer value: consequtive 4 bytes (little-endian)
2) string value: length (int value) + bytes of the string
3) BcComm operation: 
	opcode (one byte, values: 1-12) + operands
	opcodes: 
		BcPush        1
		BcPop         2
		BcGoto        3
		BcIfNot       4
		BcReadBool    5
		BcReadInt     6
		BcReadString  7
		BcPrint       8
		BcConstBool   9
		BcConstInt    10
		BcConstString 11
		BcStackOp     12

	special case - BcStackOp. It has itself operation from the list:
		not, neg, +, -, *, /, %, ==, !=, <=, <, >=, >, &&, ||, 
		i2b, i2s, b2s, b2i, s2i, s2b
		which is represented by a byte with its number in this list.

		So, BcStackOp:
			operation id (one byte)

4) BcCode(locals: int, code: [BcComm]): 
	locals (int value) + secuence of command codes from code.

*/

// Encode a program in a binary format
bcode2binary(prog: BcCode) -> string;

// Decode a program from binary string
binary2bytecode(b: string, err: Err) -> Maybe<BcCode>;

}

bcode2binary(prog: BcCode) -> string {
	op2bin = foldi(bytecode_ops, makeTree(), \i, acc, p -> setTree(acc, p.first, fromCharCode(i)));
	int2binary(prog.numLocals) + fold(prog.code, "", \acc, com ->
		acc + switch (com) {
			BcPush(i):        fromCharCode(1) + int2binary(i);
			BcPop(i):         fromCharCode(2) + int2binary(i);
			BcGoto(sh):       fromCharCode(3) + int2binary(sh);
			BcIfNot(sh):      fromCharCode(4) + int2binary(sh);
			BcReadBool(i):    fromCharCode(5) + int2binary(i);
			BcReadInt(i):     fromCharCode(6) + int2binary(i);
			BcReadString(i):  fromCharCode(7) + int2binary(i);
			BcPrint():        fromCharCode(8);
			BcConstBool(b):   fromCharCode(9)  + fromCharCode(b2i(b));
			BcConstInt(i):    fromCharCode(10) + int2binary(i);
			BcConstString(s): fromCharCode(11) + int2binary(strlen(s)) + s;
			BcStackOp(op):    fromCharCode(12) + lookupTreeDef(op2bin, op, "");
		}
	);
}

int2binary(value : int) -> string {
	fromCharCode(bitAnd(value, 0xFF)) +
	fromCharCode(bitAnd(bitUshr(value, 8), 0xFF))+
	fromCharCode(bitAnd(bitUshr(value, 16), 0xFF))+
	fromCharCode(bitAnd(bitUshr(value, 24), 0xFF));
}

binary2int(b: string, pos: int) -> int {
	getCharCodeAt(b, pos) +
	bitShl(getCharCodeAt(b, pos + 1), 8) +
	bitShl(getCharCodeAt(b, pos + 2), 16) +
	bitShl(getCharCodeAt(b, pos + 3), 24);
}

binary2bytecode(b: string, err: Err) -> Maybe<BcCode> {
	locals = binary2int(b, 0);
	bin2op = foldi(bytecode_ops, makeTree(), \i, acc, p -> setTree(acc, i, p.first));
	maybeMap(doBinary2bytecode(b, 4, [], bin2op, err), \code -> BcCode(locals, code));
}

doBinary2bytecode(b: string, i: int, acc: [BcComm], bin2op: Tree<int, string>, err: Err) -> Maybe<[BcComm]> {
	if (i == strlen(b)) Some(acc) else {
		com_id = getCharCodeAt(b, i);
		if (com_id == 1) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcPush(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 2) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcPop(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 3) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcGoto(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 4) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcIfNot(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 5) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcReadBool(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 6) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcReadInt(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 7) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcReadString(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 8) {
			doBinary2bytecode(b, i + 1, arrayPush(acc, BcPrint()), bin2op, err);
		} else if (com_id == 9) {
			doBinary2bytecode(b, i + 2, arrayPush(acc, BcConstBool(i2b(getCharCodeAt(b, i + 1)))), bin2op, err);
		} else if (com_id == 10) {
			doBinary2bytecode(b, i + 5, arrayPush(acc, BcConstInt(binary2int(b, i + 1))), bin2op, err);
		} else if (com_id == 11) {
			len = binary2int(b, i + 1);
			str = substring(b, i + 5, len);
			doBinary2bytecode(b, i + 5 + len, arrayPush(acc, BcConstString(str)), bin2op, err);
		} else if (com_id == 12) {
			op = lookupTreeDef(bin2op, getCharCodeAt(b, i + 1), "");
			doBinary2bytecode(b, i + 2, arrayPush(acc, BcStackOp(op)), bin2op, err);
		} else {
			err.report("failed to decode binary simlang bytecode");
			None();
		}
	}
}
